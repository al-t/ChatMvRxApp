buildscript {

    apply from: 'gradle/dependencies.gradle'
    ext {
        kotlin_version = '1.3.72'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath deps.gradle
        classpath deps.kotlinGradlePlugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {

    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion androidCompileSdkVersion

                defaultConfig {
                    minSdkVersion androidMinSdkVersion
                    targetSdkVersion androidTargetSdkVersion
                    versionCode 1
                    versionName "1.0"
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                buildTypes {
                    release {
                        minifyEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = '1.8'
                }
            }
        }
    }
}
